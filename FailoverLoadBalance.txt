### CONFIGURACAO PARA LOADBALANCE ENTRE 2 PROVEDORES
/ip firewall filter
add action=fasttrack-connection chain=forward connection-state=established,related,untracked hw-offload=yes

/ip firewall address-list
add address=172.16.0.0/24 comment=RedeLocal list=Local_Network

/routing table
add disabled=no fib name=to_Provedor01
add disabled=no fib name=to_Provedor02

/ip route
add disabled=no distance=2 dst-address=0.0.0.0/0 gateway=pppoe-Provedor01 pref-src="" routing-table=to_Provedor01 scope=\
    30 suppress-hw-offload=no target-scope=10
add disabled=no distance=1 dst-address=0.0.0.0/0 gateway=pppoe-Provedor02 pref-src="" routing-table=to_Provedor02 scope=\
    30 suppress-hw-offload=no target-scope=10

/ip firewall mangle
add action=accept chain=prerouting dst-address-list=Local_Network in-interface=bridge-LAN src-address-list=\
    Local_Network

add action=mark-connection chain=prerouting comment=pppoe-Provedor01 connection-mark=no-mark connection-state=new \
    dst-address=!172.16.0.0/24 dst-address-type=!local new-connection-mark=connection_Provedor01 passthrough=yes \
    per-connection-classifier=src-address-and-port:2/0 src-address=172.16.0.0/24
add action=mark-connection chain=prerouting comment=pppoe-Provedor02 connection-mark=no-mark connection-state=new \
    dst-address=!172.16.0.0/24 dst-address-type=!local new-connection-mark=connection_Provedor02 passthrough=yes \
    per-connection-classifier=src-address-and-port:2/1 src-address=172.16.0.0/24
	
add action=mark-routing chain=prerouting comment=route_Provedor01 connection-mark=connection_Provedor01 in-interface=\
    bridge-LAN new-routing-mark=to_Provedor01 passthrough=no
add action=mark-routing chain=prerouting comment=route_Provedor02 connection-mark=connection_Provedor02 in-interface=\
    bridge-LAN new-routing-mark=to_Provedor02 passthrough=no


/routing rule
add action=lookup disabled=no dst-address=172.16.0.0/24 src-address=172.16.0.0/24 table=main

### SCRIPT DE FAILOVER PARA 2 PROVEDORES
:global statusProvedor1
:global statusProvedor2
:global disableLTE
:global ignoreGateway "192.168.100.1"
:global provedorPrincipal "pppoe-Provedor01"
:global provedorSecundario "pppoe-Provedor02"
:global testeICMP {"8.8.8.8"; "1.1.1.1"; "9.9.9.9"; "4.2.2.1"; "208.67.220.220"}
:global percPING 11
:global resPING 0

foreach i in=[/ip/route/find dst-address="0.0.0.0/0" routing-table="main"] do={
  :global gateway [/ip/route/get number=$i gateway ]

  delay 2

  if ( $ignoreGateway != $gateway ) do={
    :set resPING 0

    foreach i in=$testeICMP do={
      :local resultProvedor [/ping count=3 interface=$gateway address=$i]
      :set resPING ($resPING + $resultProvedor)
    }

    delay 2

    if ( $resPING <= $percPING ) do={ 
      if ( $gateway = $provedorPrincipal ) do={
        :set statusProvedor1 "down"
        /ip/route/set distance=5 [/ip/route/find dst-address=0.0.0.0/0 gateway=$provedorPrincipal ]
        /ip/firewall/mangle/disable [find comment=$provedorPrincipal]
      } else={
        :set statusProvedor2 "down"
        /ip/firewall/mangle/disable [find comment=$provedorSecundario]
      }
    } else={ 
      if ( $gateway = $provedorPrincipal ) do={
        :set statusProvedor1 "up"
        /ip/route/set distance=1 [/ip/route/find dst-address=0.0.0.0/0 gateway=$provedorPrincipal ]
        /ip/route/set distance=2 [/ip/route/find dst-address=0.0.0.0/0 gateway=$provedorSecundario ]
        /ip/firewall/mangle/enable [find comment=$provedorPrincipal]
      } else={
        :set statusProvedor2 "up"
        /ip/firewall/mangle/enable [find comment=$provedorSecundario]
      }
    }
  }
}

delay 2

if ( "$statusProvedor1"="down" and "$statusProvedor2"="down" ) do={
  /interface/enable [find comment="LTE"]
  /ip/firewall/filter enable [find comment="WARNING"]
} else={
    :set disableLTE  [/interface/get [find name="lte1"] disable]
    delay 2
    if ( $disableLTE=false ) do={
     :put "desativado"
     /interface/disable [find comment="LTE"]
     /ip/firewall/filter disable [find comment="WARNING"]
    }
}
